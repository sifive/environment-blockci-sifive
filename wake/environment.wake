def semverMatches range version =
  range ==* version || (
    def cmdline =
      "python3", "-c", "import semver; exit(not semver.match(\"{version}\", \"{range}\"))", Nil
    makePlan cmdline Nil
    | setPlanKeep False
    | setPlanStderr logNever
    | setPlanStdout logNever
    | runJobWith defaultRunner
    | isJobOk
  )


def jString jValue = match jValue
  JString string = Pass string
  _ = Fail "not a JString".makeError

def jObject jValue = match jValue
  JObject fields = Pass fields
  _ = Fail "not a JObject".makeError

def jFieldFn fn next jValue =  match jValue.jObject
  Fail e = Fail e
  Pass fields = match (find (fn _.getPairFirst) fields)
    Some (Pair (Pair _ value) _) = next value
    None = Fail "no matching field".makeError

def jField name next jValue =
  jFieldFn (name ==* _) next jValue


def getEnvironmentFromJValue semverMatchFn resourceString jvalue =
  def toolRange = extract `([^/]*)/([^/]*)` resourceString
  def vendorToolRange = extract `([^/]*)/([^/]*)/([^/]*)` resourceString
  def fieldsResult = match toolRange vendorToolRange
    _ (vendor, tool, range, Nil) =
      jField vendor (jField tool (jFieldFn range.semverMatchFn jObject)) jvalue
    (tool, range, Nil) _=
      jField tool (jFieldFn range.semverMatchFn jObject) jvalue
    _ _ = Fail "resources string does not match [vendor/]tool/version format: {resourceString}".makeError

  def getEnvironment fields =
    def getEnvKey (Pair field val) = rmap (Pair field _) val.jString
    fields
    | map getEnvKey
    | findFail

  rmapPass getEnvironment fieldsResult


def prependOrElseSetEnv env (Pair key value) =
  def editFn =
    _
    | omap ("{value}:{_}")
    | getOrElse value
    | Some
  editEnvironment key editFn env

tuple JSONEnvironmentRunnerPlan =
  global Name: String
  global ScoreFn: Double => Double
  global SemverMatchFn: (range: String) => (version: String) => Boolean
  global JSONs: List JValue
  global BaseRunner: Runner

global def makeJSONEnvironmentRunnerPlan name scoreFn semverMatchFn jsons baseRunner =
  JSONEnvironmentRunnerPlan name scoreFn semverMatchFn jsons baseRunner

global def makeBetterJSONRunner (JSONEnvironmentRunnerPlan name scoreFn semverMatchFn jValues baseRunner) =
  def getResourceEnvPairs resources =
    def getEnv resource =
      def envResult = findPassFn (getEnvironmentFromJValue semverMatchFn resource) jValues
      Pair resource envResult
    map getEnv resources

  def getFailedResources resourceEnvPairs =
    def getResourceIfFail (Pair resource result) = match result
      Pass _ = None
      Fail _ = Some resource
    mapPartial getResourceIfFail resourceEnvPairs

  def score plan =
    def failedResources = plan.getPlanResources.getResourceEnvPairs.getFailedResources
    plan
    | setPlanResources failedResources
    | baseRunner.getRunnerScore
    | rmap scoreFn


  def pre input =
    def inputFn input =
      def resourceEnvPairs = input.getRunnerInputResources.getResourceEnvPairs
      def newEnv =
        def prependEnv env1 (Pair resource envResult) = match envResult
          Pass env2 = foldl prependOrElseSetEnv env1 env2
          Fail _ = env1
        input
        | getRunnerInputEnvironment
        | (foldl prependEnv _ resourceEnvPairs)
      def newResources = getFailedResources resourceEnvPairs
      input
      | setRunnerInputEnvironment newEnv
      | setRunnerInputEnvironment newResources
    Pair (rmap inputFn input) Unit

  def post (Pair output Unit) = output

  makeRunner name score pre post baseRunner

publish blockCIEnvironmentJSONs = source "{here}/../environment.json", Nil

publish runner =
  def jsonsResult =
    subscribe blockCIEnvironmentJSONs
    | map parseJSONFile
    | findFail
  match jsonsResult
    Fail e =
      def _ = printlnLevel logError "Failed to publish example runner: {e.getErrorCause}"
      Nil
    Pass jsons =
      def plans =
        makeJSONEnvironmentRunnerPlan "environment-json-local" (_ *. 1.5) semverMatches jsons localRunner,
        makeJSONEnvironmentRunnerPlan "environment-json-default" (_ *. 1.5) semverMatches jsons defaultRunner,
        Nil
      map makeBetterJSONRunner plans
